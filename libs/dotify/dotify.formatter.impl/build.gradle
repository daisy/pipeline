apply plugin: 'java'
apply plugin: "maven"
apply plugin: "signing"
//custom plugins
apply plugin: "org.dm.bundle"
apply plugin: "eclipse"

group = 'org.daisy.dotify'

sourceSets {
	main {	java { srcDir 'src' }	resources { srcDir 'src' } }
	test {	java { srcDir 'test' }	resources { srcDir 'test' } }
}

configurations {
	provided
	compile { extendsFrom provided }
}

/* Run gradle with JDK 1.7 or set bootstrap classpath, e.g.:
	options.bootClasspath = "/usr/lib/jvm/java-7-openjdk/jre/lib/rt.jar" */
compileJava {
    options.encoding = 'UTF-8'
    sourceCompatibility = 1.7
    targetCompatibility = 1.7
}

compileTestJava {
	options.encoding = 'UTF-8'
	sourceCompatibility = 1.7
	targetCompatibility = 1.7
}

def repoRevision = System.getenv("REPO_REVISION")!=null?System.getenv("REPO_REVISION"):repositoryRevision

javadoc {
	options.encoding = 'UTF-8'
	options.links('http://docs.oracle.com/javase/7/docs/api/')
}

repositories {
    mavenCentral()
    mavenLocal()
    //maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
}

dependencies {
    provided group: "biz.aQute.bnd", name: "annotation", version: "2.3.0"
    compile "org.daisy.dotify:dotify.api:2.9.0"
    compile "org.daisy.dotify:dotify.common:2.1.0"
    compile (group: 'org.daisy.libs', name: 'saxon-he', version: '9.5.1.5') {
		exclude module: 'Saxon-HE'
	}

    runtime 'com.fasterxml.woodstox:woodstox-core:5.0.2'
    testCompile group: "junit", name: "junit", version: "4.7"
    testCompile 'org.daisy.braille:braille-utils.api:2.0.0'
    testCompile ('org.daisy.braille:braille-utils.pef-tools:1.0.1') {
		exclude module: 'Saxon-HE'
	}
    testCompile "org.mockito:mockito-core:1.10.19"
    
	testRuntime group: group, name:'dotify.hyphenator.impl', version:'2.0.1'
	testRuntime group: group, name:'dotify.text.impl', version:'2.0.0'
	testRuntime group: group, name:'dotify.translator.impl', version:'2.3.0'
}

buildscript {
    repositories { mavenCentral() }
    dependencies { classpath group: "org.dm.gradle", name: "gradle-bundle-plugin", version: "0.6.4" }
}

task updateBndVersion() << {
	String contents = file('bnd.bnd').getText('UTF-8')
	file('bnd.bnd').write(contents.replaceAll('Bundle-Version: [\\d\\.A-Za-z\\-]+', "Bundle-Version: $version"), 'UTF-8')
}

ext.writeTestsOverviewFile = { folderPath, fileName ->
	def folder = file(folderPath)
	def f = new File(folder, fileName)
	f.write('<?xml version="1.0" encoding="UTF-8"?>\n')
	f.append('<?xml-stylesheet href="display.xslt" type="text/xsl" ?>\n')
	f.append('<tests>\n')
	FileTree list = fileTree(dir: folder)
	list.include '**/*-input.obfl'
	list.each {
		def relpath = it.path.substring(projectDir.path.length()+folderPath.length()+2)
		def exp = relpath.substring(0, relpath.length()-11) + '-expected.pef'
		if (new File(folder, exp).exists()) {
			f.append('<test input="'+relpath+'" expected="' + exp + '"/>'+'\n')
		}
	}
	f.append('</tests>')
	return f
}

javadoc.dependsOn('updateTestsOverviewFile')

task updateTestsOverviewFile() << {
	// There are more tests in this folder with another naming standard, but they
	// don't suit the presentational format and are more of integration tests.
	// Additional tests should be written based on these.
	def r = writeTestsOverviewFile('test/org/daisy/dotify/engine/impl/resource-files', 'tests-overview.xml')
	ant {
		xslt (
			basedir: "test",
			destdir: (new File("${buildDir}/docs/tests")),
			style: "build-resources/display.xslt",
			includes: "**/tests-overview.xml",
			filenameparameter: "filename"
		) {
			mapper (
				type: "package",
				from: "*.xml",
				to: "*.html"
			)
			xmlcatalog {
				dtd (
					publicId: "-//W3C//DTD XHTML 1.0 Strict//EN",
					location: "build-resources/xhtml10/xhtml1-strict.dtd"
				)
			}
			param (
				name: 'timestamp', 
				expression: new Date().format("yyyy-MM-dd'T'HH:mmXXX")
			)
		}
	}
	r.delete()
}

bundle {
    instructions << [
        "-include": file('bnd.bnd'),
        'Built-By': System.getProperty("user.name"),
        'Built-On': new Date().format('yyyy-MM-dd'),
        'Repository-Revision': "$repoRevision",
        'Repository-URL': "$repositoryURL"
    ]
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

ext.isReleaseVersion = !version.endsWith("SNAPSHOT")

signing {
    required { isReleaseVersion && gradle.taskGraph.hasTask("uploadArchives") }
    sign configurations.archives
}

uploadArchives {
	//Only upload if a git hash is supplied. On Travis only upload snapshots.
	enabled = repoRevision.size()==40 && System.getenv("TRAVIS_BUILD_ID")!=null
    repositories {
        mavenDeployer {
            if (isReleaseVersion) {
                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
            }
            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: sonatypeUsername, password: sonatypePassword)
            }
            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: sonatypeUsername, password: sonatypePassword)
            }
            pom.project {
                name 'dotify.formatter.impl'
                packaging 'jar'
                description 'Dotify Formatter'
                url "$repositoryURL"
                scm {
                    connection "$repositorySCM"
                    developerConnection "$repositorySCM"
                    url "$repositoryURL"
                }
                licenses {
                    license {
                        name 'LGPL'
                        url 'http://www.gnu.org/licenses/lgpl.html'
                    }
                }
                developers {
                    developer {
                        id 'joel'
                        name 'Joel HÃ¥kansson'
                    }
                }
            }
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.12'
}
